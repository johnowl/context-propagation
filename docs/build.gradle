plugins {
    id 'java'
    id 'io.spring.antora.generate-antora-yml' version '0.0.1'
    id 'org.antora' version '1.0.0'
    id 'java-library'
}

description = "Micrometer Context Propagation Docs"

antora {
    version = '3.2.0-alpha.2'
    playbook = 'cached-antora-playbook.yml'
    playbookProvider {
        repository = 'micrometer/micrometer-docs'
        branch = 'docs-build'
        path = 'lib/antora/templates/per-branch-antora-playbook.yml'
        checkLocalBranch = true
    }
    options = ['--clean', '--stacktrace']
    /*
    environment = [
            //'ALGOLIA_API_KEY': '82c7ead946afbac3cf98c32446154691',
            //'ALGOLIA_APP_ID': '244V8V9FGG',
            //'ALGOLIA_INDEX_NAME': 'micrometer-docs'
    ]
    */
    dependencies = [
            '@antora/atlas-extension': '1.0.0-alpha.1',
            '@antora/collector-extension': '1.0.0-alpha.3',
            '@asciidoctor/tabs': '1.0.0-beta.3',
            '@opendevise/antora-release-line-extension': '1.0.0-alpha.2',
            '@springio/antora-extensions': '1.3.0',
            '@springio/asciidoctor-extensions': '1.0.0-alpha.9'
    ]
}


tasks.named("generateAntoraYml") {
    asciidocAttributes = project.provider( {
        return  ["micrometer-context-propagation-version": project.version.toString()]
    } )
}

tasks.create("generateAntoraResources") {
    dependsOn 'generateAntoraYml'
}

tasks.named("antora") {
    dependsOn "generateAntoraResources"
}

tasks.named("antora") {
    dependsOn "test"
}

jar {
    enabled = false
}

javadoc {
    enabled = false
}

repositories {
   mavenCentral()
}

dependencies {
    implementation project(':context-propagation')

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-inline'
    testImplementation 'org.assertj:assertj-core'
}

